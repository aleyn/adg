; ADG installer based on NSIS
; Check http://nsis.sourceforge.net/ for details

!define SRCDIR      "@abs_top_srcdir@"
!define BUILDDIR    "@abs_top_builddir@"
!define USRDIR      "@USRDIR@"
!define DLLDIR      "${USRDIR}/bin"

; ---------------------------------------------------------------
; 1. Header file

!addincludedir "${SRCDIR}/build"
!include MUI2.nsh
!include EnvVarUpdate.nsh

Name "@PACKAGE_NAME@ @PACKAGE_VERSION@"
OutFile "@abs_top_builddir@/@PACKAGE@-@PACKAGE_VERSION@-win@ARCH@.exe"
SetCompressor /SOLID lzma
InstallDir "$PROGRAMFILES@ARCH@\@PACKAGE_NAME@"
RequestExecutionLevel user

; ---------------------------------------------------------------
; 2. Interface configuration

!define MUI_ABORTWARNING
!define MUI_COMPONENTSPAGE_SMALLDESC

!define MUI_ICON "${SRCDIR}/build/adg.ico"
!define MUI_UNICON "${SRCDIR}/build/adg.ico"

!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_RIGHT
!define MUI_HEADERIMAGE_BITMAP "${SRCDIR}/build/adg-header.bmp"
!define MUI_HEADERIMAGE_UNBITMAP "${SRCDIR}/build/adg-header.bmp"

!define MUI_WELCOMEFINISHPAGE_BITMAP "${SRCDIR}/build/adg-welcome.bmp"
!define MUI_UNWELCOMEFINISHPAGE_BITMAP "${SRCDIR}/build/adg-welcome.bmp"

; ---------------------------------------------------------------
; 3. Pages

!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "@abs_top_srcdir@/COPYING"
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH

; ---------------------------------------------------------------
; 4. Language files

!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "Italian"

; ---------------------------------------------------------------
; 5. Reserve files


; ---------------------------------------------------------------
; I. Installer sections

Section "ADG canvas" SecBase
  SectionIn RO

  SetOutPath "$INSTDIR"
  File /oname=adg-demo.exe  "${BUILDDIR}/demo/.libs/adg-demo-uninstalled.exe"
  File /oname=cpml-demo.exe "${BUILDDIR}/demo/.libs/cpml-demo-uninstalled.exe"

  SetOutPath "$INSTDIR\share\adg"
  File "${BUILDDIR}/demo/adg-demo.ui"
  File "${BUILDDIR}/demo/cpml-demo.ui"
  File "${SRCDIR}/demo/adg-16.png"
  File "${SRCDIR}/demo/adg-32.png"
  File "${SRCDIR}/demo/adg-48.png"
  File "${SRCDIR}/demo/adg-64.png"
  File "${SRCDIR}/demo/adg-128.png"

  SetOutPath "$INSTDIR\bin"
  File "${DLLDIR}/zlib1.dll"
  File "${DLLDIR}/libexpat-*.dll"
  File "${DLLDIR}/libintl-*.dll"
  File "${DLLDIR}/iconv.dll"
  File "${DLLDIR}/libffi-*.dll"
  File "${DLLDIR}/libpng??-*.dll"
  File "${DLLDIR}/libglib-2.0-*.dll"
  File "${DLLDIR}/libgthread-2.0-*.dll"
  File "${DLLDIR}/libgmodule-2.0-*.dll"
  File "${DLLDIR}/libgobject-2.0-*.dll"
  File "${DLLDIR}/libgio-2.0-*.dll"
  File "${DLLDIR}/libpixman-1-*.dll"
  File "${DLLDIR}/libcairo-*.dll"
  File "${DLLDIR}/libpango-1.0-*.dll"
  File "${DLLDIR}/libpangocairo-1.0-*.dll"
  File "${DLLDIR}/libpangowin32-1.0-*.dll"
  File "${DLLDIR}/libatk-1.0-*.dll"
  File "${DLLDIR}/libgdk_pixbuf-2.0-*.dll"
  File "${DLLDIR}/libgdk-3-*.dll"
  File "${DLLDIR}/libgtk-3-*.dll"
  File "${BUILDDIR}/src/cpml/.libs/libcpml-1-*.dll"
  File "${BUILDDIR}/src/adg/.libs/libadg-1-*.dll"

  SetOutPath "$INSTDIR\share\glib-2.0\schemas"
  File "${USRDIR}/share/glib-2.0/schemas/gschemas.compiled"

  CreateDirectory "$SMPROGRAMS\ADG Canvas"
  CreateShortcut  "$SMPROGRAMS\ADG Canvas\ADG demonstration program.lnk" "$INSTDIR\adg-demo.exe"
  CreateShortcut  "$SMPROGRAMS\ADG Canvas\CPML test case.lnk" "$INSTDIR\cpml-demo.exe"
  CreateShortcut  "$SMPROGRAMS\ADG Canvas\Uninstall ADG Canvas.lnk" "$INSTDIR\uninstall.exe"

  WriteUninstaller "$INSTDIR\uninstall.exe"
  ${EnvVarUpdate} $0 "PATH" "A" "HKCU" "$INSTDIR\bin"
SectionEnd

Section "Support for languages" SecLanguages
  SetOutPath "$INSTDIR\share\locale"
SectionEnd

; Descriptions

LangString DESC_SecBase ${LANG_ENGLISH} "Base installation for running the demo program."
LangString DESC_SecBase ${LANG_ITALIAN} "Installazione di base per eseguire il programma dimostrativo."
LangString DESC_SecLanguages ${LANG_ENGLISH} "Enable support for languages other than english."
LangString DESC_SecLanguages ${LANG_ITALIAN} "Abilitazione del supporto a lingue diverse dall'inglese."

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SecBase} $(DESC_SecBase)
  !insertmacro MUI_DESCRIPTION_TEXT ${SecLanguages} $(DESC_SecLanguages)
!insertmacro MUI_FUNCTION_DESCRIPTION_END

; Uninstaller

Section "Uninstall"
  Delete "$SMPROGRAMS\ADG Canvas\ADG Demonstration program.lnk"
  Delete "$SMPROGRAMS\ADG Canvas\CPML test case.lnk"
  Delete "$SMPROGRAMS\ADG Canvas\Uninstall ADG Canvas.lnk"
  RMDir  "$SMPROGRAMS\ADG Canvas"

  Delete "$INSTDIR\bin\libgtk-3-*.dll"
  Delete "$INSTDIR\bin\libgdk-3-*.dll"
  Delete "$INSTDIR\bin\libgdk_pixbuf-2.0-*.dll"
  Delete "$INSTDIR\bin\libatk-1.0-*.dll"
  Delete "$INSTDIR\bin\libpangowin32-1.0-*.dll"
  Delete "$INSTDIR\bin\libpangocairo-1.0-*.dll"
  Delete "$INSTDIR\bin\libpango-1.0-*.dll"
  Delete "$INSTDIR\bin\libcairo-*.dll"
  Delete "$INSTDIR\bin\libpixman-1-*.dll"
  Delete "$INSTDIR\bin\libgio-2.0-*.dll"
  Delete "$INSTDIR\bin\libgobject-2.0-*.dll"
  Delete "$INSTDIR\bin\libgmodule-2.0-*.dll"
  Delete "$INSTDIR\bin\libgthread-2.0-*.dll"
  Delete "$INSTDIR\bin\libglib-2.0-*.dll"
  Delete "$INSTDIR\bin\libpng??-*.dll"
  Delete "$INSTDIR\bin\libffi-*.dll"
  Delete "$INSTDIR\bin\iconv.dll"
  Delete "$INSTDIR\bin\libintl-*.dll"
  Delete "$INSTDIR\bin\libexpat-*.dll"
  Delete "$INSTDIR\bin\zlib1.dll"
  Delete "$INSTDIR\bin\libcpml-1-*.dll"
  Delete "$INSTDIR\bin\libadg-1-*.dll"
  RMDir  "$INSTDIR\bin"

  Delete "$INSTDIR\share\adg\adg-demo.ui"
  Delete "$INSTDIR\share\adg\cpml-demo.ui"
  Delete "$INSTDIR\share\adg\adg-128.png"
  Delete "$INSTDIR\share\adg\adg-64.png"
  Delete "$INSTDIR\share\adg\adg-48.png"
  Delete "$INSTDIR\share\adg\adg-32.png"
  Delete "$INSTDIR\share\adg\adg-16.png"
  RMDir  "$INSTDIR\share\adg"

  Delete "$INSTDIR\share\glib-2.0\schemas\gschemas.compiled"
  RMDir  "$INSTDIR\share\glib-2.0\schemas"
  RMDir  "$INSTDIR\share\glib-2.0"

  RMDir  "$INSTDIR\share\locale"

  RMDir  "$INSTDIR\share"

  Delete "$INSTDIR\adg-demo.exe"
  Delete "$INSTDIR\cpml-demo.exe"
  Delete "$INSTDIR\uninstall.exe"
  RMDir  "$INSTDIR"

  ${un.EnvVarUpdate} $0 "PATH" "R" "HKCU" "$INSTDIR\bin"
SectionEnd
